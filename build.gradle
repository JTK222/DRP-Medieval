buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven {url='https://dist.creeper.host/Sponge/maven'}
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "com.wynprice.cursemaven:CurseMaven:1.2.3"
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'idea'
    id 'maven-publish'
}

//The Plugin block only works for plugins in the gradle maven
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.wynprice.cursemaven'
apply plugin: 'org.spongepowered.mixin'


def MOD_VERSION = '0.5';

version = "${mc_version}-${mod_version}"
group = "net.dark_roleplay.${modid}"
archivesBaseName = "${modid}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

configurations {
    apiCompile.extendsFrom(compile)
}

sourceSets {
    api
    main.resources.srcDirs += 'src/generated/resources'
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

minecraft {
    mappings channel: 'snapshot', version: "${mapping}-${mcp_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('runs/client')
            arg "-mixin.config="+archivesBaseName+".mixins.json"
            property 'forge.logging.console.level', 'debug'

            environment 'MOD_CLASSES',
                    "archivesBaseName%%${project.file('out/production/resources').canonicalPath};" +
                            "archivesBaseName%%${project.file('out/production/classes').canonicalPath};" +
                            "archivesBaseName%%${project.file('out/api/resources').canonicalPath};"+
                            "archivesBaseName%%${project.file('out/api/classes').canonicalPath}"
        }

        server {
            workingDirectory project.file('runs/server')
            arg "-mixin.config="+archivesBaseName+".mixins.json"
            property 'forge.logging.console.level', 'debug'

            environment 'MOD_CLASSES',
                    "archivesBaseName%%${project.file('out/production/resources').canonicalPath};" +
                            "archivesBaseName%%${project.file('out/production/classes').canonicalPath};" +
                            "archivesBaseName%%${project.file('out/api/resources').canonicalPath};"+
                            "archivesBaseName%%${project.file('out/api/classes').canonicalPath}"
        }

        data {
            workingDirectory project.file('runs/data_gen')
            args '--mod', 'drpmedieval', '--all', '--output', file('src/generated/resources/')

            environment 'MOD_CLASSES',
                    "archivesBaseName%%${project.file('out/production/resources').canonicalPath};" +
                            "archivesBaseName%%${project.file('out/production/classes').canonicalPath};" +
                            "archivesBaseName%%${project.file('out/api/resources').canonicalPath};"+
                            "archivesBaseName%%${project.file('out/api/classes').canonicalPath}"
        }
    }
}

repositories {
    maven {url = "https://maven.dark-roleplay.net/"}
    maven {url = "https://minecraft.curseforge.com/api/maven/"}
    mavenLocal()
}

dependencies {
    minecraft([
            group   : 'net.minecraftforge',
            name    : 'forge',
            version : "${mc_version}-${forge_version}"

    ])

    compileOnly(fg.deobf("net.dark_roleplay:marg:1.16.1-0.5.4:api"))
    runtimeOnly(fg.deobf("net.dark_roleplay:marg:1.16.1-0.5.4"))
}

jar {
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.api.output.resourcesDir
    from sourceSets.api.output.classesDirs

    manifest {
        attributes([
            "Specification-Title": "${modid}",
            "Specification-Vendor": "Dark Roleplay",
            "Specification-Version": "1",
            "Implementation-Title": "Project Brazier",
            "Implementation-Version": "${version}",
            "Implementation-Vendor": "Dark Roleplay",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "${modid}.mixins.json"
        ])
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.api.allSource
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.api.output
}

artifacts {
    archives apiJar
    archives sourcesJar
    archives javadocJar
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }

    // Minify json resources
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

idea {
    module {
        for (String excludeDirName in ["run", "out", "runs", ".github", ".idea", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}

//curseforge {
//    apiKey = System.getenv('CURSE_API_KEY') // This should really be in a gradle.properties file
//    project {
//        id = '238326'
//        changelog = file('changelog.txt') // A file can also be set using: changelog = file('changelog.txt')
//        releaseType = 'alpha'
//        mainArtifact(jar) {
//            displayName = "DRP Medieval - ${MC_VERSION} - v${MOD_VERSION} Alpha"
//            relations {
//                requiredDependency 'marg'
//            }
//        }
//    }
//}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact reobfArtifact
            artifact apiJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url System.getenv('MVN_REPO')
            credentials {
                username System.getenv('MVN_USER')
                password System.getenv('MVN_PASSWORD')
            }
        }
    }
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}